@clearjournal
unsetalias ing
promptalias creature
if not @findalias creature
  stop
endif
while not dead self
  // move to target
  @setalias mvtarget creature
  while not inrange 'mvtarget' 2
    if @x 'mvtarget' > x 'self' and @y 'mvtarget' > y 'self'
      walk 'Southeast'
    elseif @x 'mvtarget' < x 'self' and @y 'mvtarget' > y 'self'
      walk 'Southwest'
    elseif @x 'mvtarget' > x 'self' and @y 'mvtarget' < y 'self'
      walk 'Northeast'
    elseif @x 'mvtarget' < x 'self' and @y 'mvtarget' < y 'self'
      walk 'Northwest'
    elseif @x 'mvtarget' > x 'self' and @y 'mvtarget' == y 'self'
      walk 'East'
    elseif @x 'mvtarget' < x 'self' and @y 'mvtarget' == y 'self'
      walk 'West'
    elseif @x 'mvtarget' == x 'self' and @y 'mvtarget' > y 'self'
      walk 'South'
    elseif @x 'mvtarget' == x 'self' and @y 'mvtarget' < y 'self'
      walk 'North'
    endif
    pause 200
  endwhile
  // check is taming...
  if @injournal 'You fail to tame'
    @clearjournal
    unsetalias ing
  elseif @injournal 'It seems to accept you as master.'
    break
  endif
  if @findalias ing
    continue
  endif
  // try to tame
  useskill 'Animal taming'
  target creature
  pause 25
  // check start
  if @injournal 'You start to tame'
    @clearjournal
    @setalias ing creature
  endif
endwhile
// clean
@clearjournal
unsetalias ing

